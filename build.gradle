//file:noinspection GroovyUnusedAssignment

plugins {
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'jacoco'
    id 'checkstyle'
    id 'org.springframework.boot' version '2.7.5'
    id 'java'
    id "io.freefair.lombok" version "6.5.1"
}

apply plugin: 'maven-publish'

group = 'pl.edu.pw.elka.paprykaisalami'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
}

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly

    compileOnly.extendsFrom annotationProcessor
}

dependencies {
    // Spring
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.data:spring-data-envers'

    // Nullability annotations
    implementation 'org.jetbrains:annotations:23.0.0'

    // Database Drivers
    runtimeOnly 'com.h2database:h2'

    // Unit tests
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.assertj:assertj-core'

    // Integration tests
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationTestImplementation 'org.springframework.security:spring-security-test'
    integrationTestImplementation 'org.json:json:20220924'
    integrationTestImplementation 'net.javacrumbs.json-unit:json-unit-spring:2.36.0'
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
    }
}

tasks.named('test') {
    useJUnitPlatform()
}


tasks.named('integrationTest') {
    useJUnitPlatform()
}

tasks.named('check') {
    dependsOn integrationTest
}

publishing {
    publications {
        maven(MavenPublication) {
            components.java
        }
    }
    repositories {
        maven {
            url = "http://20.4.227.77:8081/repository/gradle-nexus-repo/"
            allowInsecureProtocol = true
            credentials {
                username = System.properties['nexusUsername']
                password = System.properties['nexusPassword']
            }
        }
    }
}
